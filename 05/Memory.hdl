// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl
/** 
 * The Memory chip implements the complete address space of the Hack data memory,
 * including the RAM and memory mapped I/O.
 * Outputs the value of the memory location specified by the address input.
 * If (load == 1), sets the memory location specified by the address input
 * to the value of the in input.
 * Address space rules:
 * Only the upper 16K + 8K + 1 words of the memory are used.
 * Access to address 0 to 16383 results in accessing the RAM;
 * Access to address 16384 to 24575 results in accessing the Screen memory map;
 * Access to address 24576 results in accessing the Keyboard memory map.
 */ 
CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    Not(in=address[14], out=isRam);
    And(a=load, b=isRam, out=ramLoad);
    RAM16K(in=in, load=ramLoad, address=address[0..13], out=ramOut);

    And(a=load, b=address[14], out=screenLoad);
    Screen(in=in, load=screenLoad, address=address[0..12], out=screenOut);

    Keyboard(out=kbdOut);

    Mux16(a=screenOut, b=kbdOut, sel=address[13], out=screenOrKbd);
    Mux16(a=ramOut, b=screenOrKbd, sel=address[14], out=out);

    // This also works but not optimal since it uses 3 Mux16.
    //Mux4Way16(a=ramOut, b=ramOut, c=screenOut, d=kbdOut, sel[0]=address[13],sel[1]=address[14], out=out);
}
